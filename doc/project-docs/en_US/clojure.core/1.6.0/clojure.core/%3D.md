`(= x y)` is true if:

* Both arguments are numbers in the same 'category', and numerically
  the same, where category is one of (integer or ratio), floating
  point, or BigDecimal.  Use `==` if you want to compare for numerical
  equality between different categories, or you want an exception
  thrown if either value is not a number.
* Both arguments are sequences, lists, vectors, or queues, with `=`
  elements in the same order (including non-Clojure Java lists
  implementing `java.util.List`).
* Both arguments are sets, with `=` elements, ignoring order
  (including non-Clojure Java sets implementing `java.util.Set`).
* Both arguments are maps, with `=` key/value pairs, ignoring order
  (including non-Clojure Java maps implementing `java.util.Map`).
* Both arguments are symbols, or both keywords, with equal namespaces
  and names.
* Both arguments are the same type defined with `deftype`.  The type's
  `equiv` method is called and its return value becomes the value of
  `(= x y)`.
* Both arguments are refs, vars, or atoms, and they are the same
  object, i.e. `(identical?  x y)` is true.
* For other types, Java's `x.equals(y)` is true.  The result should be
  unsurprising for `nil`, booleans, characters, and strings.

If you call `=` with more than two arguments, the result will be true
when all consecutive pairs are `=`.

Exceptions, or possible surprises:

* When comparing collections with `=`, numbers within the collections
  are also compared with `=`, so the three numeric categories above
  are significant.
* 'Not a Number' values `Float/NaN` and `Double/NaN` are not `=` to
  any value, not even themselves.  This leads to odd behavior if you
  use them as set elements or map keys.
* Clojure regex's, e.g. #"a.*bc", are implemented using Java
  `java.util.regex.Pattern` objects, and Java's `equals` on two
  `Pattern` objects returns `(identical? re1 re2)`.  Thus `(= #"abc"
  #"abc")` returns false, and only returns true if two regex's happen
  to be the same identical object in memory.  Recommendation: Don't
  use regex's as set elements or keys.  If you feel the need to,
  consider converting them to strings first, e.g. `(str #"abc")` ->
  "abc".  http://dev.clojure.org/jira/browse/CLJ-1182 declined
* (Clojure 1.5.1) `=` is false for BigDecimal values with different
  scales, e.g. `(= 1.50M 1.500M)` is false.  Fixed in Clojure 1.6.0.
  http://dev.clojure.org/jira/browse/CLJ-1118

For the reasons above, you should not put 'Not a Number' or regex's
into Clojure collections.  You will not be able to find them using
functions like `contains?`, look them up in a map, check for their
membership in a set, nor be able to remove them.

Examples:

```clojure
user=> (= 3 3N)   ; same category integer
true
user=> (= 2 2.0)  ; different categories integer and floating point
false
user=> (= [0 1 2] '(0 1 2))
true
user=> (= '(0 1 2) '(0 1 2.0))   ; 2 and 2.0 are not =
false

;; While this map is similar to the vector in that it maps the
;; same integers 0, 1, and 2 to the same values, maps and vectors
;; are never = to each other.
user=> (= ["a" "b" "c"] {0 "a" 1 "b" 2 "c"})
false

user=> (= (with-meta #{1 2 3} {:key1 1}) (with-meta #{1 2 3} {:key1 2}))
true                  ; Metadata is ignored when comparing

user=> (= Double/NaN Double/NaN)  ; this is normal
false

user=> (def s1 #{1.0 2.0 Double/NaN})
#'user/s1
user=> s1
#{2.0 1.0 NaN}
user=> (contains? s1 1.0)         ; this is expected
true
user=> (contains? s1 Double/NaN)  ; this might surprise you
false
```
